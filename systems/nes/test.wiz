import "src/nes";
import "src/define";
import "src/math";
import "src/video";
import "src/audio";
import "src/joypad";
import "src/physics";

namespace test {

    in ram {
        var result : u8;
        var pad1, pad2 : u8;

        var previous : u8;
        var pos_x, pos_y : u8;

        var address : u16;
    }

    in program {
        const message = "HELLO  WORLD\0";

        const metatiles_sequence : [u8] = [
            0, 1, 6, 0, 10
        ];

        const metasprite : [u8] = [
             0, 0x30, 0, 0,
             0, 0x32, 0, 8,
            16, 0x34, 0, 0,
            16, 0x36, 0, 8,
        ];
    }

    inline func prepare_nametable() {

        // write a message
        video.set_ppu_address(video.NAMETABLE_CURSOR(0, 8, 12));
        video.print(&message as *u8);

        // compute a product and write the result in decimal
        b0 = a = 3;
        result = a = math.mul(&b0, 14);
        video.set_ppu_address(video.NAMETABLE_CURSOR(0, 10, 14));
        video.print_decimal(result);

        pos_x = a = 100;
        pos_y = a = 200;

        previous = a = 0;
        pad1 = a;
        pad2 = a;

        // draw metatiles on the screen
        store_address(&metatiles_address as *u16, &metatiles_sequence as u16);
        video.draw_metatiles(metatiles_address, a = 5, a = >:nes.ppu.ADDRESS_NAMETABLE_0, a = 9);

        // print the start address of the metatiles array definition
        video.set_ppu_address(video.NAMETABLE_CURSOR(0, 10, 17));
        video.print_hexadecimal(>:&metatiles);
        video.print_hexadecimal(<:&metatiles);
        // length of the metatiles array definition
        nes.ppu.data = a = ' ';
        video.print_decimal(METATILES_COUNT);

        video.set_ppu_address(video.NAMETABLE_CURSOR(0, 10, 22));
        result = a = physics.get_collision_mask(metatiles_address, a = 5, a = 1);
        video.print_hexadecimal(result); // expect 0x72

        x = 0;
        physics.bodies_x.position_high[x] = a = 50;
        physics.bodies_x.position_low [x] = a = 50;
        physics.bodies_y.position_high[x] = a = 50;
        physics.bodies_y.position_low [x] = a = 50;

        video.set_ppu_address(video.NAMETABLE_CURSOR(0, 2, 23));
        physics.body_to_screen_position(x);
        video.print_decimal(b0);
        nes.ppu.data = a = ' ';
        video.print_decimal(b1);

        audio.update_tempo();
    }

    inline func main_loop() {

        joypad.pool();
        joypad.read_inputs(&pad1, &nes.joy.input1);
        joypad.axis_horizontal (&pos_x, &pad1);
        joypad.axis_vertical   (&pos_y, &pad1);

        b0 = a = joypad.compare_to_previous(&previous, &pad1);
        previous = a = pad1;

        if { a = b0 & nes.joy.mask.A; } && !zero {
            x = pos_x;
            y = pos_y;
            pos_x = y;
            pos_y = x;
        }

        store_address(&video.data_ptr as *u16, &metasprite as u16);

        debug_marker();
        video.draw_metasprite(video.data_ptr, 0, a = 16, a = pos_x, a = pos_y, a = 1);
    }

}
