
import "src/nes";
import "src/define";

namespace joypad {

    // instantiate pooling
    inline func pool () {
		nes.joy.output = a = 1;
		nes.joy.output = a = 0;
    }

	// pool the state of both joypads
	inline func read_inputs (let storage : *u8, let input : *const u8) {
        x = nes.joy.bit.COUNT;
        do {
            // LSR: push bit 0 of input into the carry
            a = (*input) >>> 1;
            // ROR: pull carry into bit 7 of target
            (*storage) >>>>#= 1;
        } while { --x; } && !zero;
	}

    // compare the inputs to their previous state 
    // to detect if some buttons where just pressed
    inline func compare_to_previous (let previous : *const u8, let current : *const u8) : u8 in a {
        return a = ~(*previous) & (*current);
    }

    // get horizontal direction for a joypad
    inline func axis_horizontal (let storage : *u8, let joy : *const u8) {
        if (*joy) $ nes.joy.bit.LEFT  { --(*storage); }
        if (*joy) $ nes.joy.bit.RIGHT { ++(*storage); }
    }

    // get vertical direction for a joypad
    inline func axis_vertical (let storage : *u8, let joy : *const u8) {
        // BIT '$' only support bits 7 and 6
        // in that case, we have to use regular bitmasking
        if { a = (*joy) & nes.joy.mask.UP  ; } && !zero { --(*storage); }
        if { a = (*joy) & nes.joy.mask.DOWN; } && !zero { ++(*storage); }
    }

}